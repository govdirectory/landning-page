# expected_result_count: 50
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q183 AS ?country)

  VALUES ?type {
    wd:Q1221156 # state of Germany (16)
    wd:Q896375 # federal ministry (15)
    wd:Q22721 # governmental districts (10)
    wd:Q829277 # governmental districts of Nordrhein-Westfalen (5)
    wd:Q42612577 # governmental districts of Baden-WÃ¼rttemberg (4)
  }

  ?org wdt:P31 ?type ;
       wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,mul". }
}
ORDER BY ?type ?topic
