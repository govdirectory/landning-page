# expected_result_count: 21
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
WHERE {
  BIND(wd:Q159 AS ?country)

  VALUES ?type {
    wd:Q4481741 # ministries (21)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P576 ?ddate .
         MINUS { ?org wdt:P31 wd:Q4481741 ;
                       wdt:P576 ?ddate ;
                       wdt:P571 ?idate .
                FILTER ( ?idate > ?ddate )
               }
         }          

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,ru" }
}
ORDER BY ?type ?orgLabel
