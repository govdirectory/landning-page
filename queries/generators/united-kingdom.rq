# expected_result_count: 23
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q145 AS ?country)

  VALUES ?type {
    wd:Q2500378 # department of the United Kingdom Government (23)
    wd:Q21451686 # local authorities (32)
    wd:Q83425186 # NHS boards (14)
    wd:Q20667323 # Health and social care partnerships (31)
  }

  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
}
ORDER BY ?qid ?topic
