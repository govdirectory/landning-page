SELECT
  ?uri
  ?name
  ?safeName
  ?description
  (SAMPLE(?website) AS ?website)
  (SAMPLE(?nativeLabel) AS ?nativeLabel)
  (GROUP_CONCAT(DISTINCT ?typeOfGovLabel; separator=",") AS ?typeOfGovList)
  ?headOfStateLabel
  ?headOfGovLabel
WHERE {
  VALUES (?uri ?name ?safeName ?description) {
    (wd:Q34 'Sweden' 'sweden' 'All Swedish government agencies are included.')
    (wd:Q145 'United Kingdom' 'united-kingdom' 'Current content includes ministerial departments and Scottish exceutive agencies, local authorities, NHS boards, NDPBs, courts, public corporations, tribunals, parole boards, Queen\'s printer, non-ministerial government departments, commissioners, ombudsmen and Health and social care partnerships.')
    (wd:Q223 'Greenland' 'greenland' 'Current content only includes municipalities.')
    (wd:Q35 'Denmark' 'denmark' 'Current content includes, ministries, regions and municipalities.')
    (wd:Q20 'Norway' 'norway' 'Current content only includes municipalities, ministries and embassies.')
    (wd:Q117 'Ghana' 'ghana' 'Current content only includes regions and districts.')
    (wd:Q183 'Germany' 'germany' 'Current content only includes ministries, federated states and governmental districts.')
    (wd:Q258 'South Africa' 'south-africa' 'Current content includes departments, independent constitutional bodies, provinces and municipalities.')
    (wd:Q55 'Netherlands' 'netherlands' 'Current content includes ministries, courts, water boards, the Kings cabinet, provincies and municipalities.')
    (wd:Q664 'New Zealand' 'new-zealand' 'Current content includes district health boards, wƒÅnangas, state-owned enterprises, regional councils and territorial authorities.')
    (wd:Q33 'Finland' 'finland' 'Current content includes regions.')
    (wd:Q212 'Ukraine' 'ukraine' 'Current content includes ministries and first level administrative units.')
    (wd:Q159 'Russia' 'russia' 'Current content includes the government and ministries.')
    (wd:Q574 'East Timor' 'east-timor' 'Current content includes ministries, municipalities, special administrative region, administrative posts and sucos.')
    (wd:Q928 'Philippines' 'philippines' 'Current content includes departments, regions, provinces and cities.')
    (wd:Q668 'India' 'india' 'Current content includes ministries, states and union territories.')
    (wd:Q31 'Belgium' 'belgium' 'Current content includes federal scientific institutes, public institutions of social security, federal public sevices, public planning services, language areas, communities, regions, extant provinces, police zones, emergency zones and municipalities.')
  }

  OPTIONAL {
    ?uri wdt:P856 ?website .
    ?uri wdt:P37 ?lang .
    OPTIONAL {
      ?uri p:P856 ?ws .
      ?ws pq:P407 ?language .
      FILTER (?language IN  (?lang, wd:Q1860 ))
    }
  }
  OPTIONAL { ?uri wdt:P35 ?headOfState }
  OPTIONAL { ?uri wdt:P6 ?headOfGov }
  OPTIONAL { ?uri wdt:P122 ?typeOfGov }
  OPTIONAL { ?uri wdt:P1705 ?nativeLabel }
  
  SERVICE wikibase:label {
    # this might need to be updated when new countries are added
    bd:serviceParam wikibase:language "en,sv" .
    ?headOfState rdfs:label ?headOfStateLabel .
    ?headOfGov rdfs:label ?headOfGovLabel .
    ?typeOfGov rdfs:label ?typeOfGovLabel .
  }
}
GROUP BY ?uri ?name ?safeName ?description ?headOfGovLabel ?headOfStateLabel
ORDER BY ?name
